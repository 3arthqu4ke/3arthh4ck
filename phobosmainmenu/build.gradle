buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {
            name = 'SpongePowered'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            name = "jitpack.io"
            url = "https://jitpack.io"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = '1.0.0'
group = 'me.earth.phobosmainmenu' 
archivesBaseName = 'phobosmainmenu' 

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = '1.12.2-14.23.5.2768'
    runDir = 'run'
    mappings = 'stable_39'
    makeObfSourceJar = false
}

repositories {
    mavenCentral()
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven/'
    }

    maven {
        name = "jitpack.io"
        url  = "https://jitpack.io"
    }

    jcenter()
}

dependencies {
    compileOnly('com.github.3arthqu4ke:3arthh4ck:1.7.7') {
        exclude module: 'mixin'
    }

    compile('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }
}

classes {
    // always build the jar, so we always have the
    // latest version in the plugins folder
    finalizedBy(jar)
}

task copyJar(type: Copy, dependsOn: jar) {
    from(jar)
    into project.file('run/earthhack/plugins')
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, 'mixins.phobosmainmenu.refmap.json' 
}

jar {
    manifest {
        attributes(
            'MixinConfigs': 'mixins.phobosmainmenu.json', 
            '3arthh4ckConfig': 'PhobosMainMenuPluginConfig.json' 
        )
    }
    finalizedBy(copyJar)
}
